import net.fabricmc.loom.task.RemapJarTask

plugins {
    id "dev.architectury.loom" version "1.0-SNAPSHOT"
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
    id 'maven-publish'
}

apply plugin: 'dev.architectury.loom'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.minecraft_version+'-'+project.mod_version
group = project.maven_group

sourceSets {
    api{
        java {
            compileClasspath += main.compileClasspath
        }
    }
    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}

repositories {
    allprojects {
        repositories {
            maven { url 'https://jitpack.io' }
        }
    }
    maven { //AutoConfig
        name = "Shedaniel's Maven"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/moreculling.accesswidener")

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfigs "moreculling.mixins.json"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"

    // ConditionalMixinForged
    modImplementation "com.github.TexTrueStudio:ConditionalMixinForged:${project.conditional_mixins_version}"
    include "com.github.TexTrueStudio:ConditionalMixinForged:${project.conditional_mixins_version}"

    // Rubidium Compat
    modImplementation "maven.modrinth:rubidium:${project.rubidium_version}"
    // ^ Change to `modCompileOnly` to test without rubidium

    //AutoConfig
    modCompileOnly "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"
    //include "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"
    // ^ Comment out to test without fabric api, this will require you to put the cloth-config in the mods folder
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(AbstractArchiveTask) {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}

tasks.withType(JavaCompile) {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

tasks.withType(Jar) {
    from "LICENSE"
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    classifier "api-dev"
    from sourceSets.api.output
}

task remapApiJar(type: RemapJarTask) {
    classifier "api"

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}
prepareRemapApiJar.dependsOn(apiJar)

// configure the maven publication
publishing {
    publications {
        main (MavenPublication) {
            artifactId "moreculling"
            artifact(remapJar)
        }

        api (MavenPublication) {
            artifactId "moreculling-api"
            artifact(remapApiJar)
        }
    }
}

build.dependsOn remapApiJar